module us{
module ihmc{
module robotDataLogger{
	struct YoRegistryDefinition
	{
		/**
		 * Id of the registry
		 *
		 * This is a long long field to allow generating unique ID's per thread/process to support future dynamic registries.
		 */
		long long id;
		long long parent;
		string name;
	};
	
	enum YoType
	{
		DoubleYoVariable,
		BooleanYoVariable,
		IntegerYoVariable,
		LongYoVariable,
		EnumYoVariable
	};
	
	enum JointType
	{
		SiXDoFJoint,
		OneDoFJoint
	};
	
	enum LoadStatus
	{
		NoParameter,
		Unloaded,
		Default,
		Loaded
	};
	
	struct YoVariableDefinition
	{
		long long id;
		string name;
		string description;
		YoType type;
		long long registry;
		unsigned short enumType;
		boolean allowNullValues;
		boolean isParameter;
		double min;
		double max;
		LoadStatus loadStatus;
	};
	
	struct JointDefinition
	{
		string name;
		JointType type;
		
	};
	
	struct SCS1AppearanceDefinitionMessage
	{
		double r;
		double g;
		double b;
		
		double transparency;
	};
	
	struct SCS1YoGraphicObjectMessage
	{
		long long registrationID;
		string name;
		
		sequence<long long, 1024> yoVariableIndex;
		sequence<double, 128> constants;
		SCS1AppearanceDefinitionMessage appearance;
		string listName;
	};

    struct SCS2YoGraphicDefinitionMessage
    {
        sequence<string, 64> fieldNames;
        sequence<string, 64> fieldValues;
    }

	struct ReferenceFrameInformation
	{
		sequence<unsigned long, 8192> frameIndices;
		sequence<string, 8192> frameNames;
	};

	struct EnumType
	{
		string name;
		sequence<string, 255> enumValues;
	};
	
	struct Summary{
	   	boolean createSummary;
   	   	string<1024> summaryTriggerVariable;
   	 	sequence<string<1024>, 128> summarizedVariables;
	};
	
	struct Handshake
	{
		/** 
		  * Set to true if ID's are explicitly defined for registries/variables. 
		  * This field is to allow for backwards compatibility with previous logs.
		  */
		bool hasExplicitId;
	
		double dt;
		sequence<YoRegistryDefinition, 1024> registries;
		sequence<YoVariableDefinition, 32767> variables;
		sequence<JointDefinition, 128> joints;
		sequence<SCS1YoGraphicObjectMessage, 2048> graphicObjects;
		sequence<SCS1YoGraphicObjectMessage, 2048> artifacts;
		sequence<SCS2YoGraphicDefinitionMessage, 2048> scs2YoGraphicDefinitions;
		sequence<EnumType, 1024> enumTypes;
		ReferenceFrameInformation referenceFrameInformation;
		Summary summary;
	};

};
};
};
